
plugins {
    id "com.diffplug.gradle.spotless" version "3.26.1"
    id "io.freefair.lombok" version "5.1.0"
}

apply plugin: 'java'
apply plugin: 'jacoco'
apply plugin: 'maven-publish'
apply plugin: 'signing'

repositories {
    mavenCentral()
}

// group is necessary
group = 'io.github.nicolas-van'
// the current version
version = '0.0.6'

spotless {
  java {
    googleJavaFormat('1.7').aosp()
  }
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.7.0'
}

test {
    testLogging {
        events "passed", "skipped", "failed"
    }
    useJUnitPlatform()
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

javadoc.failOnError = false

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives jar
    archives sourcesJar
    archives javadocJar
}

signing {
    def signingKey = System.getenv("MVN_PGP_PRIVATE_KEY")
    def signingPassword = System.getenv("MVN_PGP_PRIVATE_KEY_PASSWORD")
    useInMemoryPgpKeys(signingKey, signingPassword)
    sign configurations.archives
}


def pomConfig = {
    licenses {
        // the license
        license {
            name "MIT"
            url "https://opensource.org/licenses/MIT"
            distribution "repo"
        }
    }
    developers {
        developer {
            id "nicolas-van"
            name "Nicolas Vanhoren"
        }
    }
    
    scm {
        // the vcs url
       url "https://github.com/nicolas-van/eventemitter"
    }
}

publishing {
    publications {
        MyPublication(MavenPublication) {
            from(components.java)
            artifact sourcesJar
            artifact javadocJar
            
            groupId rootProject.group
            artifactId rootProject.name
            version rootProject.version
            pom.withXml {
                def root = asNode()
                // the description of the project
                root.appendNode('description', 'A simple library implementing an event emitter in Java.')
                root.appendNode('name', rootProject.name)
                // the name of the project
                root.appendNode('url', 'https://github.com/nicolas-van/eventemitter#readme')
                root.children().last() + pomConfig
            }
        }
    }
    repositories {
        maven {
            url "https://oss.sonatype.org/service/local/staging/deploy/maven2"
            credentials {
                username System.getenv("SONATYPE_USERNAME")
                password System.getenv("SONATYPE_PASSWORD")
            }
        }
    }
}
