
plugins {
    id "com.diffplug.gradle.spotless" version "3.26.1"
    id "io.freefair.lombok" version "5.1.0"
    id "com.jfrog.bintray" version "1.8.5"
}

apply plugin: 'java'
apply plugin: 'jacoco'
apply plugin: 'maven-publish'

// group is necessary
group = 'eventemitter'
// the current version
version = '0.0.4'

repositories {
    jcenter()
}

spotless {
  java {
    googleJavaFormat('1.7').aosp()
  }
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.7.0'
}

test {
    testLogging {
        events "passed", "skipped", "failed"
    }
    useJUnitPlatform()
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

javadoc.failOnError = false

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

def pomConfig = {
    licenses {
        // the license
        license {
            name "MIT"
            url "https://opensource.org/licenses/MIT"
            distribution "repo"
        }
    }
    developers {
        developer {
            id "nicolas-van"
            name "Nicolas Vanhoren"
        }
    }
    
    scm {
        // the vcs url
       url "https://github.com/nicolas-van/eventemitter"
    }
}

publishing {
    publications {
        MyPublication(MavenPublication) {
            from(components.java)
            artifact sourcesJar
            artifact javadocJar
            
            groupId rootProject.group
            artifactId rootProject.name
            version rootProject.version
            pom.withXml {
                def root = asNode()
                // the description of the project
                root.appendNode('description', 'A simple library implementing an event emitter in Java.')
                root.appendNode('name', rootProject.name)
                // the name of the project
                root.appendNode('url', 'https://github.com/nicolas-van/eventemitter#readme')
                root.children().last() + pomConfig
            }
        }
    }
}

bintray {
    // the user name in bintray
    user = 'nicolas-van'
    // the bintray api key
    key = System.getenv("BINTRAY_API_KEY")
    publications = ['MyPublication']
    publish = true
    
    pkg {
        // the repo name in bintray
        repo = 'maven'
        name = rootProject.name
        // the license again
        licenses = ['MIT']
        // the vcs url again
        vcsUrl = 'https://github.com/nicolas-van/eventemitter'
        pkg {
            version {
                name = rootProject.version
            }
        }
    }
}