
plugins {
    id "com.diffplug.gradle.spotless" version "3.26.1"
    id "io.freefair.lombok" version "5.1.0"
    id "com.jfrog.bintray" version "1.8.5"
}

apply plugin: 'java'
apply plugin: 'jacoco'
apply plugin: 'maven-publish'

group = 'eventemitter'
version = '0.0.3'

repositories {
    jcenter()
}

spotless {
  java {
    googleJavaFormat('1.7').aosp()
  }
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.7.0'
}

test {
    testLogging {
        events "passed", "skipped", "failed"
    }
    useJUnitPlatform()
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

javadoc.failOnError = false

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

publishing {
    publications {
        MyPublication(MavenPublication) {
            from(components.java)
            artifact sourcesJar
            artifact javadocJar
            
            groupId rootProject.group
            artifactId rootProject.name
            version rootProject.version
        }
    }
}

bintray {
    user = 'nicolas-van'
    key = System.getenv("BINTRAY_API_KEY")
    publications = ['MyPublication']
    publish = true
    
    pkg {
        repo = 'maven'
        name = rootProject.name
        licenses = ['MIT']
        vcsUrl = 'https://github.com/nicolas-van/eventemitter'
        pkg {
            version {
                name = rootProject.version
            }
        }
    }
}